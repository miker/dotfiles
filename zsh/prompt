#!/bin/zsh -f

# set the titlebar to the current working directory
#chpwd()

# format titles for screen and rxvt
function title() {
    # escape '%' chars in $1, make nonprintables visible
    a=${(V)1//\%/\%\%}

    # Truncate command, and join lines.
    a=$(print -Pn "%40>...>$a" | tr -d "\n")

    case $TERM in
        screen-*)
        print -Pn "\ek$a:$3\e\\"      # screen title (in ^A")
        ;;
        xterm*|rxvt-*)
        print -Pn "\e]2;$2 | $a:$3\a" # plain xterm title
        ;;
    esac
}

# precmd is called just before the prompt is printed
function precmd() {
    title "zsh" "$USER@%m" "%55<...<%~"
}

# preexec is called just before any command line is executed
function preexec() {
    title "$1" "$USER@%m" "%35<...<%~"
}


for zshrc_snipplet in ~/.zsh/prompt_extras/S[0-9][0-9]*[^~] ; do
   source $zshrc_snipplet
done
if (( EUID == 0 )); then
    PROMPT=$'%{\e[01;31m%}%n@%m%{\e[0m%}[%{\e[01;34m%}%3~%{\e[0;m%}](%?)$(get_git_prompt_info)%# '
else
    PROMPT=$'%{\e[01;32m%}%n@%m%{\e[0m%}[%{\e[01;34m%}%3~%{\e[0;m%}](%?)$(get_git_prompt_info)%% '
fi


# vim: set et sw=4 sts=4 ts=4 ft=zsh :
